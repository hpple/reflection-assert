def reflectionAssertVersion = '0.3'

ext {
    junitVersion = '5.6.2'
    guavaVersion = '25.1-jre'
    ognlVersion = '3.1.4'
    opentest4jVersion = '1.2.0'

    isReleaseVersion = !reflectionAssertVersion.endsWith('-SNAPSHOT')

    ossrhReleasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    ossrhSnapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

subprojects {
    group 'com.github.hpple.reflection-assert'
    version reflectionAssertVersion

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion

        testImplementation group: 'com.google.guava', name: 'guava', version: guavaVersion
    }

    test {
        useJUnitPlatform()
    }

    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = 'Reflection Assert'
                    description = 'Reflection-based assertions for JUnit 5'
                    url = 'https://github.com/hpple/reflection-assert'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        // Original Unitils authors
                        developer {
                            name = 'Tim Ducheyne'
                        }
                        developer {
                            name = 'Filip Neven'
                        }
                        // Author of the fork
                        developer {
                            id = 'hpple'
                            name = 'Stanislav Kashirin'
                            email = 'kashirinst@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/hpple/reflection-assert.git'
                        developerConnection = 'scm:git:https://github.com/hpple/reflection-assert.git'
                        url = 'https://github.com/hpple/reflection-assert/tree/master'
                    }
                }
            }
        }

        repositories {
            maven {
                name = 'MavenCentral'
                url = isReleaseVersion ? ossrhReleasesRepoUrl : ossrhSnapshotsRepoUrl
                credentials {
                    username = "${project.findProperty('ossrhUsername')}"
                    password = "${project.findProperty('ossrhPassword')}"
                }
            }
        }
    }

    signing {
        sign publishing.publications.maven
    }
}
